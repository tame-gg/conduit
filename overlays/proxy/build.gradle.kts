import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar
import com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer
import java.net.URI
import java.nio.file.Files
import java.nio.file.StandardCopyOption
import java.security.MessageDigest

plugins {
    application
    id("velocity-ctd-publish")
    id("velocity-init-manifest")
    alias(libs.plugins.shadow)
}

application {
    mainClass.set("com.velocitypowered.proxy.Velocity")
    applicationDefaultJvmArgs += listOf("-Dvelocity.packet-decode-logging=true")
}

val relocations = mapOf(
    "org.bstats" to "com.velocitypowered.proxy.bstats",
)

val relocatedLibraries: Configuration by configurations.creating {
    isCanBeResolved = true
    isCanBeConsumed = false
}
// Keep the relocated libraries on the compile/runtime classpath so the proxy compiles against them
// and the fat shadowJar continues to bundle them.
configurations.named("implementation") { extendsFrom(relocatedLibraries) }

// ── Conduit: bundled spark Velocity profiler ─────────────────────────────────
// Conduit ships the official lucko/spark Velocity plugin inside the proxy jar and installs it on
// first run (see BundledSparkInstaller). We download it at build time and verify its checksum so
// the binary never lives in source control.
val bundledSparkUrl = providers.gradleProperty("conduit.spark.velocity.url")
val bundledSparkSha256 = providers.gradleProperty("conduit.spark.velocity.sha256")
val bundledSparkJar = layout.buildDirectory.file("conduit/bundled/spark-velocity.jar")

val downloadBundledSpark by tasks.registering {
    inputs.property("url", bundledSparkUrl)
    inputs.property("sha256", bundledSparkSha256)
    outputs.file(bundledSparkJar)

    doLast {
        val target = bundledSparkJar.get().asFile
        target.parentFile.mkdirs()
        val tmp = target.resolveSibling("${target.name}.tmp")

        URI(bundledSparkUrl.get()).toURL().openStream().use { input ->
            tmp.outputStream().use { output ->
                input.copyTo(output)
            }
        }

        val actualSha256 = MessageDigest.getInstance("SHA-256")
            .digest(tmp.readBytes())
            .joinToString("") { "%02x".format(it) }
        check(actualSha256 == bundledSparkSha256.get()) {
            "Downloaded spark Velocity jar checksum mismatch: " +
                "expected ${bundledSparkSha256.get()}, got $actualSha256"
        }

        Files.move(
            tmp.toPath(),
            target.toPath(),
            StandardCopyOption.REPLACE_EXISTING
        )
    }
}

tasks {
    jar {
        manifest {
            attributes["Implementation-Title"] = "Velocity-CTD"
            attributes["Implementation-Vendor"] = "Velocity(-CTD) Contributors"
            attributes["Multi-Release"] = "true"
            attributes["Enable-Native-Access"] = "ALL-UNNAMED"
            attributes["Enable-Final-Field-Mutation"] = "ALL-UNNAMED"
        }
    }

    processResources {
        // Embed :velocity-luckperms-integration as META-INF/velocityctd/integrations/velocity-luckperms-integration.jar
        val lpJar = project(":velocity-luckperms-integration")
            .tasks
            .named<Jar>("jar")
        from(lpJar.flatMap { it.archiveFile }) {
            into("META-INF/velocityctd/integrations")
            rename { "velocity-luckperms-integration.jar" }
        }

        // Conduit: embed the verified spark Velocity jar so it can be installed on first run.
        dependsOn(downloadBundledSpark)
        from(bundledSparkJar) {
            into("com/velocitypowered/proxy/conduit/bundled")
            rename { "spark-velocity.jar" }
        }
    }

    shadowJar {
        filesMatching("META-INF/org/apache/logging/log4j/core/config/plugins/**") {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
        }

        transform(Log4j2PluginsCacheFileTransformer::class.java)

        // Exclude all the collection types we don't intend to use
        exclude("it/unimi/dsi/fastutil/booleans/**")
        exclude("it/unimi/dsi/fastutil/bytes/**")
        exclude("it/unimi/dsi/fastutil/chars/**")
        exclude("it/unimi/dsi/fastutil/doubles/**")
        exclude("it/unimi/dsi/fastutil/floats/**")
        exclude("it/unimi/dsi/fastutil/longs/**")
        exclude("it/unimi/dsi/fastutil/shorts/**")

        // Exclude the fastutil IO utilities - we don't use them.
        exclude("it/unimi/dsi/fastutil/io/**")

        // Exclude most of the int types - Object2IntMap have a values() method that returns an
        // IntCollection, and we need Int2ObjectMap
        exclude("it/unimi/dsi/fastutil/ints/*Int2Boolean*")
        exclude("it/unimi/dsi/fastutil/ints/*Int2Byte*")
        exclude("it/unimi/dsi/fastutil/ints/*Int2Char*")
        exclude("it/unimi/dsi/fastutil/ints/*Int2Double*")
        exclude("it/unimi/dsi/fastutil/ints/*Int2Float*")
        exclude("it/unimi/dsi/fastutil/ints/*Int2Int*")
        exclude("it/unimi/dsi/fastutil/ints/*Int2Long*")
        exclude("it/unimi/dsi/fastutil/ints/*Int2Short*")
        exclude("it/unimi/dsi/fastutil/ints/*Int2Reference*")
        exclude("it/unimi/dsi/fastutil/ints/IntAVL*")
        exclude("it/unimi/dsi/fastutil/ints/IntArrayF*")
        exclude("it/unimi/dsi/fastutil/ints/IntArrayI*")
        exclude("it/unimi/dsi/fastutil/ints/IntArrayL*")
        exclude("it/unimi/dsi/fastutil/ints/IntArrayP*")
        exclude("it/unimi/dsi/fastutil/ints/IntArraySet*")
        exclude("it/unimi/dsi/fastutil/ints/*IntBi*")
        exclude("it/unimi/dsi/fastutil/ints/Int*Pair")
        exclude("it/unimi/dsi/fastutil/ints/IntLinked*")
        exclude("it/unimi/dsi/fastutil/ints/IntList*")
        exclude("it/unimi/dsi/fastutil/ints/IntHeap*")
        exclude("it/unimi/dsi/fastutil/ints/IntOpen*")
        exclude("it/unimi/dsi/fastutil/ints/IntRB*")
        exclude("it/unimi/dsi/fastutil/ints/IntSorted*")
        exclude("it/unimi/dsi/fastutil/ints/*Priority*")
        exclude("it/unimi/dsi/fastutil/ints/*BigList*")

        // Try to exclude everything BUT Object2Int{LinkedOpen,Open,CustomOpen}HashMap
        exclude("it/unimi/dsi/fastutil/objects/*ObjectArray*")
        exclude("it/unimi/dsi/fastutil/objects/*ObjectAVL*")
        exclude("it/unimi/dsi/fastutil/objects/*Object*Big*")
        exclude("it/unimi/dsi/fastutil/objects/*Object2Boolean*")
        exclude("it/unimi/dsi/fastutil/objects/*Object2Byte*")
        exclude("it/unimi/dsi/fastutil/objects/*Object2Char*")
        exclude("it/unimi/dsi/fastutil/objects/*Object2Double*")
        exclude("it/unimi/dsi/fastutil/objects/*Object2Float*")
        exclude("it/unimi/dsi/fastutil/objects/*Object2IntArray*")
        exclude("it/unimi/dsi/fastutil/objects/*Object2IntAVL*")
        exclude("it/unimi/dsi/fastutil/objects/*Object2IntRB*")
        exclude("it/unimi/dsi/fastutil/objects/*Object2Long*")
        exclude("it/unimi/dsi/fastutil/objects/*Object2Object*")
        exclude("it/unimi/dsi/fastutil/objects/*Object2Reference*")
        exclude("it/unimi/dsi/fastutil/objects/*Object2Short*")
        exclude("it/unimi/dsi/fastutil/objects/*ObjectRB*")
        exclude("it/unimi/dsi/fastutil/objects/*Reference*")

        // Exclude Checker Framework annotations
        exclude("org/checkerframework/checker/**")

        relocations.forEach { (from, to) -> relocate(from, to) }

        // Include Configurate 3
        val configurateBuildTask = project(":deprecated-configurate3").tasks.named("shadowJar")
        dependsOn(configurateBuildTask)
        from(zipTree(configurateBuildTask.map { it.outputs.files.singleFile }))
    }

    // A minimal shaded jar containing the proxy classes plus relocated copies of `relocatedLibraries`
    // (and nothing else). The bootstrap embeds this as the proxy jar, while resolving every other
    // dependency from Maven.
    register<ShadowJar>("proxyRelocatedJar") {
        archiveClassifier.set("relocated")
        from(sourceSets["main"].output)
        configurations = listOf(relocatedLibraries)
        relocations.forEach { (from, to) -> relocate(from, to) }
    }

    runShadow {
        workingDir = file("run").also(File::mkdirs)
        standardInput = System.`in`
        jvmArgs("-Dvelocity.packet-decode-logging=true")
    }
    named<JavaExec>("run") {
        workingDir = file("run").also(File::mkdirs)
        standardInput = System.`in` // Doesn't work?
    }

    withType<JavaCompile>().configureEach {
        options.compilerArgs.addAll(
            listOf(
                "-Alog4j.graalvm.groupId=${project.group}",
                "-Alog4j.graalvm.artifactId=${project.name}"
            )
        )
    }
}

dependencies {
    implementation(project(":velocity-api"))
    implementation(project(":velocity-native"))

    implementation(libs.bundles.log4j)
    implementation(libs.kyori.ansi)
    implementation(libs.netty.codec)
    implementation(libs.netty.codec.haproxy)
    implementation(libs.netty.codec.http)
    implementation(libs.netty.handler)
    implementation(libs.netty.transport.native.epoll)
    implementation(variantOf(libs.netty.transport.native.epoll) { classifier("linux-x86_64") })
    implementation(variantOf(libs.netty.transport.native.epoll) { classifier("linux-aarch_64") })
    implementation(libs.netty.transport.native.iouring)
    implementation(variantOf(libs.netty.transport.native.iouring) { classifier("linux-x86_64") })
    implementation(variantOf(libs.netty.transport.native.iouring) { classifier("linux-aarch_64") })
    implementation(libs.netty.transport.native.kqueue)
    implementation(variantOf(libs.netty.transport.native.kqueue) { classifier("osx-x86_64") })
    implementation(variantOf(libs.netty.transport.native.kqueue) { classifier("osx-aarch_64") })

    implementation(libs.lettuce.core)
    implementation(libs.httpclient5)
    implementation(libs.jopt)
    implementation(libs.terminalconsoleappender)
    implementation(libs.jline.terminal)
    implementation(libs.jline.reader)
    runtimeOnly(libs.jline.terminal.jni)
    runtimeOnly(libs.jline.terminal.ffm)
    runtimeOnly(libs.disruptor)
    implementation(libs.fastutil)
    implementation(platform(libs.adventure.bom))
    implementation(libs.adventure.text.serializer.json.legacy.impl)
    implementation(libs.adventure.facet)
    implementation(libs.completablefutures)
    implementation(libs.component)
    implementation(libs.nightconfig)
    relocatedLibraries(libs.bstats)
    implementation(libs.lmbda)
    implementation(libs.asm)
    implementation(libs.bundles.flare)
    implementation(libs.uuid.creator)
    compileOnly(libs.spotbugs.annotations)
    compileOnly(libs.auto.service.annotations)
    testImplementation(libs.mockito)

    annotationProcessor(libs.auto.service)
    annotationProcessor(libs.log4j.core)
}
